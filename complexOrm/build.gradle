plugins {
    id 'org.jetbrains.kotlin.multiplatform'
}
apply plugin: "com.android.library"
apply plugin: 'kotlin-android-extensions'
// Kotlin annotation processor
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 15
        //noinspection OldTargetApi
        targetSdkVersion 28
        versionCode 1
        versionName "1.1"
    }
    buildTypes {
        release {
            minifyEnabled true
            consumerProguardFiles 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.manifest.srcFile("src/androidMain/AndroidManifest.xml")
        main.java.srcDir("src/androidMain/kotlin")
        test.java.srcDir("src/androidTest/kotlin")
    }
}
version = "1.1"
kotlin {
    jvm()
    android('android')
    linuxX64("linux") {
        compilations.main.cinterops {
            sqlite3
            uuid
        }
        binaries { sharedLib { baseName = "complexOrm" } }
    }
    iosX64("ios") {
        compilations.main.cinterops {
            sqlite3
        }
        binaries { framework() }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')

                implementation project(":complexOrmApi")
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')

                implementation project(":complexOrmApi")
            }
        }
        androidMain {
            dependencies {
                implementation kotlin('stdlib')

                implementation "org.xerial:sqlite-jdbc:3.28.0"
                implementation 'net.danlew:android.joda:2.10.2'
            }
        }
        androidTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')

                implementation "org.xerial:sqlite-jdbc:3.28.0"
                implementation 'net.danlew:android.joda:2.10.2'

                implementation  "org.robolectric:robolectric:4.3"
            }
        }
        iosMain { }
        iosTest { }
        linuxMain { }
        linuxTest { }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                //noinspection GradleDependency
                implementation "joda-time:joda-time:2.10.2"
                implementation "org.xerial:sqlite-jdbc:3.28.0"
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
    }
}

dependencies {
    kaptTest project(":complexOrmProcessor")
}

task "copyGeneratedFiles" {
    dependsOn "kaptReleaseUnitTestKotlinAndroid"
    dependsOn "kaptDebugUnitTestKotlinAndroid"
    doLast {
        exec {
            commandLine "bash", "copyGeneratedFiles.bash"
        }
    }
}

task "generateIosFiles" {
    dependsOn "copyGeneratedFiles"
    doLast {
        exec {
            commandLine "bash", "generateIos.bash"
        }
    }
}

task iosTest {
    dependsOn "generateIosFiles"
    def device = project.findProperty("iosDevice")?.toString() ?: "iPhone 8"
    dependsOn kotlin.targets.ios.binaries.getTest('DEBUG').linkTaskName
    group = JavaBasePlugin.VERIFICATION_GROUP
    description = "Runs tests for target 'ios' on an iOS simulator"

    doLast {
        def binary = kotlin.targets.ios.binaries.getTest('DEBUG').outputFile
        exec {
            commandLine 'xcrun', 'simctl', 'spawn', device, binary.absolutePath
        }
    }
}
