plugins {
    id 'org.jetbrains.kotlin.multiplatform'
}

version '1.1'

sourceSets {
    main.java.srcDir("src/androidMain/kotlin")
    test.java.srcDir("src/androidTest/kotlin")
}

kotlin {
    jvm("android")
    linuxX64("linux") {
        compilations.main.cinterops {
            uuid
        }
        binaries { sharedLib { baseName = "complexOrmApi" } }
    }
    iosX64("ios") { binaries { framework() } }
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        androidMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
            }
        }
        androidTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
        iosMain { }
        iosTest { }
        linuxMain { }
        linuxTest { }
    }
}

task "generateIosFiles" {
    doLast {
        exec {
            commandLine "bash", "generateIos.bash"
        }
    }
}

task iosTest {
    dependsOn "generateIosFiles"
    def device = project.findProperty("iosDevice")?.toString() ?: "iPhone 8"
    dependsOn kotlin.targets.ios.binaries.getTest('DEBUG').linkTaskName
    group = JavaBasePlugin.VERIFICATION_GROUP
    description = "Runs tests for target 'ios' on an iOS simulator"

    doLast {
        def binary = kotlin.targets.ios.binaries.getTest('DEBUG').outputFile
        exec {
            commandLine 'xcrun', 'simctl', 'spawn', device, binary.absolutePath
        }
    }
}

configurations {
    compileClasspath
}